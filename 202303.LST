10 DIMIN$(3)
20 OPEN 1,8,0,"2023031.TXT,P,R"     : REM read input data
30 GOSUB 250
40 GOSUB 250
50 IN$(1) = RPT$(ASC("."),LEN(IN$(2)))
60 FORX=1 TO LEN(IN$(2))
70 X$=MID$(IN$(2),X,1)
80 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN XS=X:XM=X:N=0:GOSUB 130
90 NEXT X
100 IFETHEN CLOSE1:END
110 GOSUB250
120 GOTO 60
130 N = N+ASC(X$) - ASC("0")
140 X = X+1
150 X$=MID$(IN$(2),X,1)
160 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN N = N*10:XM=X:  GOTO 130
170 FOR X1=XS-1 TO XM+1
180 IF MID$(IN$(1),X1,1)<>"." THEN SP = SP+N
190 IF MID$(IN$(3),X1,1)<>"." THEN SP = SP+N
200 NEXT X1
210 IF MID$(IN$(2),XS-1,1)<>"." THEN SP = SP+N
220 IF MID$(IN$(2),XM+1,1)<>"." THEN SP = SP+N
230 PRINTN, SP
240 RETURN
250 IN$(1)=IN$(2)
260 IN$(2)=IN$(3)
270 IF ST<64 THEN LINPUT#1,IN$(3): IN$(3) = "."+IN$(3)+".": RETURN
280 IN$(3) =RPT$(ASC("."),LEN(IN$(2)))
290 E=1
300 RETURN





10 TS=TI
20 DIM IN$(3): DIM G%(255,$3F): L%=-1
30 OPEN 1,8,0,"2023031.TXT,P,R"
40 GOSUB 370
50 GOSUB 370
60 IN$(1) = RPT$(ASC("."),LEN(IN$(2)))
70 FORX=1 TO LEN(IN$(2))
80 X$=MID$(IN$(2),X,1)
90 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN XS=X:XM=X:N=0:GOSUB 150
100 NEXT X
120 IFETHEN : PRINT "Completed in";(TI-TS)/60;"s":  CLOSE1:END
130 GOSUB370
140 GOTO 70
150 N = N+ASC(X$) - ASC("0")
160 X = X+1
170 X$=MID$(IN$(2),X,1)
180 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN N = N*10:XM=X:GOTO 150
190 REM //
200 REM // Find symbols in row up XU$ and down XD$
210 FOR X1=XS-1 TO XM+1
220 XU$=MID$(IN$(1),X1,1)
230 XD$=MID$(IN$(3),X1,1)
240 IF XU$<>"." OR XD$<>"." THEN SP = SP+N
250 IFXU$="*" THENGX=X1:GY=L%-1:GOSUB 470
260 IFXD$="*" THENGX=X1:GY=L%+1:GOSUB 470
270 NEXT X1
280 REM //
290 REM // Find symbols left XL$ and right XR$
300 XL$=MID$(IN$(2),XS-1,1)
310 XR$=MID$(IN$(2),XM+1,1)
320 IF XL$<>"." OR XR$<>"." THEN SP = SP+N
330 IFXL$="*" THENGX=XS-1:GY=L%:GOSUB 470
340 IFXR$="*" THENGX=XM+1:GY=L%:GOSUB 470
350 PRINTN, SP
360 RETURN
370 REM --------
380 REM -- Shift IN$ up and read to EOF
390 L%=L%+1
400 FORI=0 TO255: G%( I, (L+1) AND $3F) = 0: NEXTI
401 FORI=0 TO255: G%( I, (L+2) AND $3F) = 0: NEXTI
402 FORI=0 TO255: G%( I, (L+3) AND $3F) = 0: NEXTI
410 IN$(1)=IN$(2)
420 IN$(2)=IN$(3)
430 IF ST<64 THEN LINPUT#1,IN$(3): IN$(3) = "."+IN$(3)+".": RETURN
440 IN$(3) =RPT$(ASC("."),LEN(IN$(2)))
450 E=1
460 RETURN
470 REM --------
480 REM -- Process Gears (GX, GY, N)
490 PRINT"*(";GX";"GY"):"N
500 GG% =G%( GX, GY AND$3F )
510 IF GG% > 0 THEN SG =SG+ GG%*N: PRINT "**";SG: RETURN
520 G%(GX,GY AND$3F)=N
530 RETURN
