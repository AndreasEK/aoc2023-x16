10 DIMIN$(3)
20 OPEN 1,8,0,"2023031.TXT,P,R"     : REM read input data
30 GOSUB 250
40 GOSUB 250
50 IN$(1) = RPT$(ASC("."),LEN(IN$(2)))
60 FORX=1 TO LEN(IN$(2))
70 X$=MID$(IN$(2),X,1)
80 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN XS=X:XM=X:N=0:GOSUB 130
90 NEXT X
100 IFETHEN CLOSE1:END
110 GOSUB250
120 GOTO 60
130 N = N+ASC(X$) - ASC("0")
140 X = X+1
150 X$=MID$(IN$(2),X,1)
160 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN N = N*10:XM=X:  GOTO 130
170 FOR X1=XS-1 TO XM+1
180 IF MID$(IN$(1),X1,1)<>"." THEN SP = SP+N
190 IF MID$(IN$(3),X1,1)<>"." THEN SP = SP+N
200 NEXT X1
210 IF MID$(IN$(2),XS-1,1)<>"." THEN SP = SP+N
220 IF MID$(IN$(2),XM+1,1)<>"." THEN SP = SP+N
230 PRINTN, SP
240 RETURN
250 IN$(1)=IN$(2)
260 IN$(2)=IN$(3)
270 IF ST<64 THEN LINPUT#1,IN$(3): IN$(3) = "."+IN$(3)+".": RETURN
280 IN$(3) =RPT$(ASC("."),LEN(IN$(2)))
290 E=1
300 RETURN





10 TS=TI: GM%=$03
20 DIM IN$(3): DIM G%(255,GM%): L=-1
30 OPEN 1,8,0,"2023031.TXT,P,R"
40 GOSUB 400
50 GOSUB 400
60 D=ASC("."): IN$(1) = RPT$(D,LEN(IN$(2)))
70 FORX=1 TO LEN(IN$(2))
80 X$=MID$(IN$(2),X,1)
90 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN XS=X:XM=X:N=0:GOSUB 150
100 NEXT X
110 PRINT,,L;"SP:" SP "SG:" SG
120 IFETHEN : PRINT "Completed in";(TI-TS)/60;"s":  CLOSE1:END
130 GOSUB400
140 GOTO 70
150 N = N+ASC(X$) - ASC("0")
160 X = X+1
170 X$=MID$(IN$(2),X,1)
180 IF ASC("0") <= ASC(X$) AND ASC(X$) <= ASC("9") THEN N = N*10:XM=X:GOTO 150
190 REM //
200 REM // Find symbols in row up XU$ and down XD$
210 FOR X1=XS-1 TO XM+1
220 XU$=MID$(IN$(1),X1,1)
230 XD$=MID$(IN$(3),X1,1)
240 IF XU$<>"." THEN SP = SP+N: PRINT XU$ " (" X1 ";" L-1 ")" N
250 IF XD$<>"." THEN SP = SP+N: PRINT XD$" ("X1 ";" L+1 ")";N
260 IF XU$="*" THEN GX=X1:GY=L-1:GOSUB 500
270 IF XD$="*" THEN GX=X1:GY=L+1:GOSUB 500
280 NEXT X1
290 REM //
300 REM // Find symbols left XL$ and right XR$
310 XL$=MID$(IN$(2),XS-1,1)
320 XR$=MID$(IN$(2),XM+1,1)
330 IF XL$<>"." THEN SP = SP+N: PRINT XL$+" (";XS-1";"L")";N
340 IF XR$<>"." THEN SP = SP+N: PRINT XR$+" (";XM+1";"L")";N
350 IF XL$="*" THENGX=XS-1:GY=L:GOSUB 490
360 IF XR$="*" THENGX=XM+1:GY=L:GOSUB 490
370 RETURN
380 REM --------
390 REM -- Shift IN$ up and read to EOF
400 L=L+1
410 FORI=0 TO255: G%( I, (L+1) AND GM%) = 0:NEXTI
420 REMG%( I, (L+2) AND $3F) = 0
430 REMG%( I, (L+3) AND $3F) = 0: NEXTI
440 IN$(1)=IN$(2)
450 IN$(2)=IN$(3)
460 IF ST<64 THEN LINPUT#1,IN$(3): IN$(3) = "."+IN$(3)+".": RETURN
470 E=1:IN$(3) =RPT$(ASC("."),LEN(IN$(2)))
480 RETURN
490 REM --------
500 REM -- Process Gears (GX, GY, N)
510 GG% =G%( GX, GY AND GM%)
520 IF GG% > 0 THEN PRINT "**(" GX ";" GY ")" GG%; N:SG =SG+ GG%*N: RETURN
530 G%(GX,GY AND GM%)=N
540 RETURN